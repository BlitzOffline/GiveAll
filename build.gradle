import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
}

group 'com.blitzoffline'
version '0.0.5'

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

compileKotlin {
    processResources.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    kotlinOptions.javaParameters = true
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.mattstudios.me/artifactory/public' }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation 'me.mattstudios.utils:matt-framework:1.4.6'
    implementation 'me.mattstudios:triumph-config:1.0.5-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.21'

    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
}

shadowJar {
    minimize()
    relocate 'kotlin', 'com.blitzoffline.giveall.libs.kotlin'
    relocate 'me.mattstudios.mf', 'com.blitzoffline.giveall.libs.commands'
    relocate 'me.mattstudios.config', 'com.blitzoffline.giveall.libs.config'
    relocate 'net.kyori', 'com.blitzoffline.giveall.libs.adventure'
    archiveFileName.set("GiveAll-${project.version}.jar")
}